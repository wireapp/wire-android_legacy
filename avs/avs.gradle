String avsPublicVersion = "6.4.233"
String avsCustomVersion = "6.4.12"
String avsPublicGroup = "com.wire"
String avsCustomGroup = "com.wearezeta.avs"
String nexusUrl = "http://10.10.124.11:8081/nexus/content/groups/public"

// Set shouldUseCustomAvs value to true if want to test with a custom AVS version in dev, internal or experimental builds
boolean shouldUseCustomAvs = false

def isConnected = !"curl -i -m 3 $nexusUrl".execute().err.text.contains("timed out")

if (isConnected) {
    for (String taskName : gradle.startParameter.taskNames) {
        if (taskName.contains("Prod") || taskName.contains("Candidate") || taskName == "aPR") {
            shouldUseCustomAvs = true
            break
        }
    }
}

String avsGroup
String avsName = System.getenv("AVS_NAME") ?: "avs"
String avsVersion

if (shouldUseCustomAvs) {
    avsVersion = System.getenv("AVS_VERSION") ?: avsCustomVersion
    avsGroup = System.getenv("AVS_GROUP") ?: avsCustomGroup
    allprojects {
        repositories {
            maven { url nexusUrl }
        }
    }
} else {
    avsVersion = System.getenv("AVS_VERSION") ?: avsPublicVersion
    avsGroup = System.getenv("AVS_GROUP") ?: avsPublicGroup
}

ext {
    avsDependency = "${avsGroup}:${avsName}:${avsVersion}"
}