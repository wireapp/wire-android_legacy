String avsPublicVersion = "6.4.233@aar"
String avsCustomVersion = "6.4.12@aar"
String avsPublicGroup = "com.wire"
String avsCustomGroup = "com.wearezeta.avs"

// Set shouldUseCustomAvs value to true if want to test with an AVS custom version in dev, internal or experimental builds
boolean shouldUseCustomAvs = false

for (String taskName : gradle.startParameter.taskNames) {
    if (taskName.contains("Prod") || taskName.contains("Candidate") || taskName == "aPR") {
        shouldUseCustomAvs = true
        break
    }
}

String avsName = System.getenv("AVS_NAME") ?: "avs"
String avsVersion
String avsGroup

if (shouldUseCustomAvs) {
    avsVersion = System.getenv("AVS_VERSION") ?: avsCustomVersion
    avsGroup = System.getenv("AVS_GROUP") ?: avsCustomGroup
} else {
    avsVersion = System.getenv("AVS_VERSION") ?: avsPublicVersion
    avsGroup = System.getenv("AVS_GROUP") ?: avsPublicGroup
}

ext {
    avsDependency = "${avsGroup}:${avsName}:${avsVersion}"
}