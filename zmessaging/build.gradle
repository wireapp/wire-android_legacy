apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'jp.leafytree.android-scala'
apply from: 'deflake.gradle'

// Android
android {
    compileSdkVersion Versions.COMPILE_SDK_VERSION
    buildToolsVersion Versions.BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Versions.MIN_SDK_VERSION
        targetSdkVersion Versions.TARGET_SDK_VERSION

        ndk {
            abiFilters "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
        }

    }

    externalNativeBuild {
        ndkBuild {
            path file('src/main/jni/Android.mk')
        }
    }

    testOptions {

        unitTests.all {

            jvmArgs "-Xmx4096M", "-XX:+CMSClassUnloadingEnabled",
                "-Djava.net.preferIPv4Stack=true", "-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=n"
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
                exceptionFormat = 'full'
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ["-source", "1.7", "-target", "1.7", "-encoding", "UTF-8"]
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.additionalParameters = ["-feature", "-target:jvm-1.7", "-Xfuture", //"-Xfatal-warnings",
                                                "-deprecation", "-encoding", "UTF-8", "-Xmax-classfile-name", "128"]
}

def versions = [
        okHttp : Versions.OKHTTP,
        circe : "0.9.3",
]

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/releases" }
}

configurations {
    cryptobox
}

dependencies {

    api project(':macrosupport')

    implementation project(ModuleDependencies.storage)

    implementation BuildDependencies.kotlin.standardLibrary
    implementation BuildDependencies.androidX.coreKtx

    api "org.scala-lang.modules:scala-async_${LegacyDependencies.SCALA_MAJOR_VERSION}:0.9.7"
    implementation "com.squareup.okhttp3:okhttp:$versions.okHttp"
    // should match okhttp3's mockserver version (see test dependencies)
    implementation BuildDependencies.libPhoneNumber
    implementation "com.wire:cryptobox-android:1.1.3"
    implementation "com.wire:generic-message-proto:1.36.0"
    implementation "io.circe:circe-core_${LegacyDependencies.SCALA_MAJOR_VERSION}:$versions.circe"
    implementation "io.circe:circe-generic_${LegacyDependencies.SCALA_MAJOR_VERSION}:$versions.circe"
    implementation "io.circe:circe-parser_${LegacyDependencies.SCALA_MAJOR_VERSION}:$versions.circe"
    implementation "com.googlecode.mp4parser:isoparser:1.1.7"
    implementation BuildDependencies.wireSignals
    implementation BuildDependencies.wireSignalsExtensions
    implementation BuildDependencies.libSodium

    //Provided dependencies
    compileOnly "com.softwaremill.macwire:macros_${LegacyDependencies.SCALA_MAJOR_VERSION}:2.3.3"
    compileOnly "com.softwaremill.macwire:macrosakka_${LegacyDependencies.SCALA_MAJOR_VERSION}:2.3.3"
    compileOnly "com.softwaremill.macwire:util_${LegacyDependencies.SCALA_MAJOR_VERSION}:2.3.3"
    compileOnly "com.softwaremill.macwire:proxy_${LegacyDependencies.SCALA_MAJOR_VERSION}:2.3.3"
    compileOnly avsDependency

    compileOnly "androidx.legacy:legacy-support-v4:1.0.0"
    compileOnly BuildDependencies.threetenbpJava
    compileOnly "net.java.dev.jna:jna:4.4.0@aar"
    compileOnly TestDependencies.robolectric
    compileOnly BuildDependencies.androidX.roomRuntime

    //Test dependencies
    testImplementation(LegacyDependencies.scalaTest) {
        exclude module: 'scala-library'
    }
    testImplementation "org.scalamock:scalamock_${LegacyDependencies.SCALA_MAJOR_VERSION}:4.1.0"
    testImplementation "org.scalacheck:scalacheck_${LegacyDependencies.SCALA_MAJOR_VERSION}:1.14.0"
    testImplementation("com.wire:robotest_${LegacyDependencies.SCALA_MAJOR_VERSION}:1.0.1") {
        exclude(group: "org.scalatest", module: "scalatest")
    }
    testImplementation TestDependencies.robolectric
    testImplementation BuildDependencies.androidX.roomRuntime
    testImplementation TestDependencies.jUnit   //to override version included in robolectric
    testImplementation "com.squareup.okhttp3:mockwebserver:$versions.okHttp"
    //should match okhttp version.
    testImplementation "org.apache.httpcomponents:httpclient:4.5.5"
    testImplementation "com.typesafe.akka:akka-http_${LegacyDependencies.SCALA_MAJOR_VERSION}:10.1.8"
    testImplementation "com.typesafe.akka:akka-actor_${LegacyDependencies.SCALA_MAJOR_VERSION}:2.5.22"
    testImplementation "com.typesafe.akka:akka-stream_${LegacyDependencies.SCALA_MAJOR_VERSION}:2.5.22"
    testImplementation  avsDependency
    testImplementation "androidx.legacy:legacy-support-v4:1.0.0"
    testImplementation BuildDependencies.threetenbpJava
    testImplementation "net.java.dev.jna:jna:4.4.0@aar"
    testImplementation TestDependencies.kotlin.coroutinesTest

    implementation "androidx.sqlite:sqlite:2.0.1"
    implementation "androidx.sqlite:sqlite-framework:2.0.1"

    cryptobox "com.wire:cryptobox-android:1.1.3@aar"
}

task extractLibSodium(type: Copy) {
    println("Extracting libsodium from cryptobox-android")
    from 'src/dist'
    from {
        zipTree(configurations.cryptobox.singleFile).matching { include 'jni/***/libsodium.so' }
    }
    into layout.buildDirectory.dir("libsodium")
}

android.libraryVariants.all { variant ->
    tasks.findByName("externalNativeBuild${variant.name.capitalize()}")
        .dependsOn(extractLibSodium)
}
