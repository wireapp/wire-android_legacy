final String AVS_GROUP_ENV_VAR = "AVS_GROUP"
final String AVS_NAME_ENV_VAR = "AVS_NAME"
final String AVS_VERSION_ENV_VAR = "AVS_VERSION"
final String NEXUS_URL_ENV_VAR = "NEXUS_URL"
final String NEXUS_URL_LOCAL_VAR = "nexus.url"
final String LOCAL_PROPERTIES_FILE_NAME = "local.properties"
final String AVS_PUBLIC_GROUP = "com.wire"
final String AVS_PRIVATE_GROUP = "com.wearezeta.avs"
final String AVS_NAME = "avs"

String avsPublicVersion = "6.6.239"
String avsPrivateVersion = "6.7.9"

String nexusUrl = ""

Properties properties = new Properties()
def propertiesFile = project.rootProject.file(LOCAL_PROPERTIES_FILE_NAME)
if (propertiesFile.exists()) {
    properties.load(propertiesFile.newDataInputStream())
    nexusUrl = System.getenv(NEXUS_URL_ENV_VAR) ?: properties.getProperty(NEXUS_URL_LOCAL_VAR) ?: ""
} else {
    nexusUrl = System.getenv(NEXUS_URL_ENV_VAR) ?: ""
}

if (!nexusUrl.isEmpty()) {
    allprojects {
        repositories {
            maven { url nexusUrl }
        }
    }
    ext {
        avsGroup = System.getenv(AVS_GROUP_ENV_VAR) ?: AVS_PRIVATE_GROUP
        avsName = System.getenv(AVS_NAME_ENV_VAR) ?: AVS_NAME
        avsVersion = System.getenv(AVS_VERSION_ENV_VAR) ?: avsPrivateVersion
        avsDependency = "${avsGroup}:${avsName}:${avsVersion}"
    }

} else {
    ext {
        avsGroup = System.getenv(AVS_GROUP_ENV_VAR) ?: AVS_PUBLIC_GROUP
        avsName = System.getenv(AVS_NAME_ENV_VAR) ?: AVS_NAME
        avsVersion = System.getenv(AVS_VERSION_ENV_VAR) ?: avsPublicVersion
        avsDependency = "${avsGroup}:${avsName}:${avsVersion}"
    }
}
