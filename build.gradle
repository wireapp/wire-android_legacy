// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    final String NEXUS_URL_ENV_VAR = "NEXUS_URL"
    final String NEXUS_URL_LOCAL_VAR = "nexus.url"
    final String LOCAL_PROPERTIES_FILE_NAME = "local.properties"

    String nexusUrl = ""
    Properties properties = new Properties()
    def propertiesFile = project.rootProject.file(LOCAL_PROPERTIES_FILE_NAME)
    if (propertiesFile.exists()) {
        properties.load(propertiesFile.newDataInputStream())
        nexusUrl = System.getenv(NEXUS_URL_ENV_VAR) ?: properties.getProperty(NEXUS_URL_LOCAL_VAR) ?: ""
    } else {
        nexusUrl = System.getenv(NEXUS_URL_ENV_VAR) ?: ""
    }

    if (!nexusUrl.isEmpty()) {
        buildscript {
            repositories {
                maven { url nexusUrl }
            }
        }
        allprojects {
            repositories {
                maven { url nexusUrl }
            }
        }

        ext {
            nexusUrlExt = nexusUrl
        }
    }

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${Versions.ANDROID_GRADLE_PLUGIN}"
        classpath "com.wire:gradle-android-scala-plugin:${Versions.SCALA_BUILD_PLUGIN}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Versions.KOTLIN}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:${Versions.KOTLIN}"
        classpath "com.google.gms:google-services:${Versions.GMS}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${Versions.DETEKT}"
        classpath "org.jacoco:org.jacoco.core:${Versions.JACOCO}"
        classpath "com.mutualmobile.gradle.plugins:dexinfo:${Versions.DEX_INFO}"
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.13.1"
    id "io.gitlab.arturbosch.detekt" version "1.2.2"
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

ext {
    activeSpeakers = true
    joinGroupCallMuted = true
    removeJoinLeaveSounds = true
    appLockFeatureFlag = true
    zooming = true
    sessionReset = true
    //Update kotlinCore flag as well when adding new kotlin features
    kotlinSettings = false
    kotlinCore = kotlinSettings
}

apply from: "./scripts/avs.gradle"
apply from: "./scripts/detekt.gradle"
apply from: "./scripts/jacoco.gradle"
apply from: "./scripts/licensing.gradle"
apply from: "./scripts/customization.gradle"
apply from: "./scripts/ci.gradle"
