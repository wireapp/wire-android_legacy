// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        fdroidBuild = gradle.startParameter.taskRequests.toString().toLowerCase().contains("fdroid") // FIXME
        println("F-Droid build: ${fdroidBuild}")
    }
    repositories {
        google()
        mavenCentral()
        jcenter() // FIXME
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${Versions.ANDROID_GRADLE_PLUGIN}"
        classpath "com.wire:gradle-android-scala-plugin:${Versions.SCALA_BUILD_PLUGIN}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Versions.KOTLIN}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:${Versions.KOTLIN}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${Versions.DETEKT}"
        classpath "org.jacoco:org.jacoco.core:${Versions.JACOCO}"
        classpath "com.mutualmobile.gradle.plugins:dexinfo:${Versions.DEX_INFO}"

        if (fdroidBuild) {
            println("Not including gms")
        } else {
            println("Including gms")
            classpath "com.google.gms:google-services:${Versions.GMS}"
        }
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.13.1"
    id "io.gitlab.arturbosch.detekt" version "1.2.2"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter() // FIXME
        maven { url "https://jitpack.io" }
        // maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
}

ext {
    federationUserDiscovery = false
    conferenceCallingRestriction = true
    webLinkPreview = true
}

apply from: "./scripts/avs.gradle"
apply from: "./scripts/detekt.gradle"
apply from: "./scripts/jacoco.gradle"
apply from: "./scripts/licensing.gradle"

apply from: "./scripts/customization.gradle"
if (buildtimeConfiguration == null) {
    apply from: "./scripts/custombuild.gradle"
}

apply from: "./scripts/ci.gradle"
