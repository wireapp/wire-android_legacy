apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'jp.leafytree.android-scala'
apply plugin: 'hugo'
apply plugin: 'com.mutualmobile.gradle.plugins.dexinfo'
apply from: 'config/quality.gradle'


repositories {
    flatDir {
        dirs 'libs'
    }
}

ext {
    majorVersion = "3.35."

    scalaMajorVersion = '2.11'
    scalaVersion = scalaMajorVersion + '.12'

    supportLibVersion = '27.1.1'
    playServicesVersion = '15.0.1'
    audioVersion = System.getenv("AUDIO_VERSION") ?: '1.209.0@aar'
    stethoVersion = '1.5.0'
    zMessagingVersion = "142.0.2316"
    paging_version = "1.0.0"

    avsVersion = '4.9.170@aar'
    avsInternalVersion = avsVersion //leave this here in case we want to test specific AVS versions on internal
    avsName = 'avs'
    avsGroup = 'com.wire'

    // proprietary avs artifact configuration
    customAvsVersion = '5.0.35@aar'
    customAvsInternalVersion = customAvsVersion
    customAvsName = 'avs'
    customAvsGroup = 'com.wearezeta.avs'

    zMessagingDevVersion = System.getenv("LOCAL_ZMESSAGING_VERSION") ?: ((System.getenv("ZMESSAGING_VERSION") ?: zMessagingVersion) + buildSuffixForVersion(zMessagingVersion))
    zMessagingReleaseVersion = System.getenv("LOCAL_ZMESSAGING_VERSION") ?: ((System.getenv("ZMESSAGING_VERSION") ?: zMessagingVersion) + '@aar')
}

if (project.file('user.gradle').exists()) {
    apply from: "user.gradle"
}

task copyCustomResources(type: Copy) {
    if (!customConfig.exists() || gradle.startParameter.taskNames.contains("clean")) {
        return
    }

    def iconFolder = (config.iconFolder ?: 'icons')
    def targetResFolder = "$rootDir/app/src/main/res"
    def iconSubfolders = [
            'ldpi': 'drawable-ldpi',
            'mdpi': 'drawable-mdpi',
            'hdpi': 'drawable-hdpi',
            'xhdpi': 'drawable-xhdpi',
            'xxhdpi': 'drawable-xxhdpi',
            'xxxhdpi': 'drawable-xxxhdpi'
    ]

    def resources = [
            "ic_launcher_wire.png" : (config.launcherIcon ?: '')
    ]

    resources.findAll { targetFileName, sourceFileName -> !sourceFileName.empty }
            .collectEntries { targetFileName, sourceFileName ->
                iconSubfolders.collectEntries { sourceSub, targetSub ->
                    [(customDir + '/' + iconFolder + '/' + sourceSub + '/' + sourceFileName) : [(targetResFolder + '/' + targetSub),  targetFileName]]
                }
            }
            .each { sourcePath, targetPath ->
                def source = file(sourcePath)
                assert source.exists()
                copy {
                    from source
                    rename { String filename -> return targetPath[1] }
                    into file(targetPath[0])
                }
                project.logger.info("Copied $sourcePath to ${targetPath[0]}/${targetPath[1]}")
            }

    copy {
        from file(customDir + '/strings/strings.xml') into file(targetResFolder + '/values')
        from file(customDir + '/strings/strings_no_translate.xml') into file(targetResFolder + '/values')
    }

    config.languages.each { code ->
        def targetStringDir = "$targetResFolder/values-$code"
        copy {
            mkdir targetStringDir
            from file("$customDir/strings/$code/strings.xml") into file(targetStringDir)
        }
    }
}

android {
    //Trigger the licenseFormat task at least once in any compile phase
    applicationVariants.all { variant ->
        variant.javaCompiler.dependsOn(rootProject.licenseFormat)
    }

    preBuild.dependsOn(copyCustomResources)

    compileSdkVersion = 27
    buildToolsVersion = '28.0.3'

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 99999
        versionName majorVersion + android.defaultConfig.versionCode
        applicationId "com.waz.zclient"
        testInstrumentationRunner "com.waz.background.TestRunner"
        manifestPlaceholders = [customURLScheme: config.custom_url_scheme]

        buildConfigField 'Integer', 'MAX_ACCOUNTS',                     "$config.maxAccounts"
        buildConfigField 'String',  'BACKEND_URL',                      "\"$config.backendUrl\""
        buildConfigField 'String',  'WEBSOCKET_URL',                    "\"$config.websocketUrl\""
        buildConfigField 'boolean', 'ACCOUNT_CREATION_ENABLED',         "$config.allow_account_creation"
        buildConfigField 'boolean',  'ALLOW_SSO',                       "$config.allowSSO"
        buildConfigField 'String',  'SUPPORT_EMAIL',                    "\"$config.supportEmail\""
        buildConfigField 'String',  'FIREBASE_PUSH_SENDER_ID',          "\"$config.firebasePushSenderId\""
        buildConfigField 'String',  'FIREBASE_APP_ID',                  "\"$config.firebaseAppId\""
        buildConfigField 'String',  'FIREBASE_API_KEY',                 "\"$config.firebaseApiKey\""
        buildConfigField 'boolean', 'ENABLE_BLACKLIST',                 "$config.enableBlacklist"
        buildConfigField 'String',  'BLACKLIST_HOST',                   "\"$config.blacklistHost\""
        buildConfigField 'String',  'CERTIFICATE_PIN_DOMAIN',           "\"$config.certificatePin.domain\""
        buildConfigField 'String',  'CERTIFICATE_PIN_BYTES',            "\"$config.certificatePin.certificate\""
        buildConfigField 'boolean', 'SUBMIT_CRASH_REPORTS',             "$config.submitCrashReports"
        buildConfigField 'boolean', 'ALLOW_MARKETING_COMMUNICATION',    "$config.allowMarketingCommunication"
        buildConfigField 'boolean', 'ALLOW_CHANGE_OF_EMAIL',            "$config.allowChangeOfEmail"
        buildConfigField 'String',  'CUSTOM_URL_SCHEME',                "\"$config.custom_url_scheme\""
        buildConfigField 'Integer', 'NEW_PASSWORD_MINIMUM_LENGTH',      "$config.new_password_minimum_length"
        buildConfigField 'Integer', 'NEW_PASSWORD_MAXIMUM_LENGTH',      "$config.new_password_maximum_length"
        buildConfigField 'String',  'TEAMS_URL',                        "\"$config.teamsUrl\""
        buildConfigField 'String',  'ACCOUNTS_URL',                     "\"$config.accountsUrl\""
        buildConfigField 'String',  'WEBSITE_URL',                      "\"$config.websiteUrl\""
        buildConfigField 'String',  'HTTP_PROXY_URL',                   "\"$config.http_proxy_url\""
        buildConfigField 'String',  'HTTP_PROXY_PORT',                  "\"$config.http_proxy_port\""
        buildConfigField 'boolean', 'BLOCK_ON_JAILBREAK_OR_ROOT',       "$config.block_on_jailbreak_or_root"
    }

    packagingOptions {
        // Need to exclude all armeabi/mips libs
        // as not all of our libs support these architectures
        exclude 'lib/armeabi/libgnustl_shared.so'
        exclude 'lib/armeabi/libspotify_embedded_shared.so'
        exclude 'lib/armeabi/libspotify_sdk.so'
        exclude 'lib/mips/librs.blur.so'
        exclude 'lib/mips/librs.decode.so'
        exclude 'lib/mips/librsjni.so'
        exclude 'lib/mips/libRSSupport.so'
        exclude 'lib/armeabi/libjnidispatch.so'
        exclude 'lib/mips/libjnidispatch.so'
        exclude 'lib/mips64/libjnidispatch.so'
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            // All the usual Gradle options.
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    compileOptions {
        compileSdkVersion rootProject.ext.compileSdkVersion
        buildToolsVersion rootProject.ext.buildToolsVersion
    }

    if (file("signing.gradle").exists()) {
        apply from: 'signing.gradle'
    }

    dexOptions {
        preDexLibraries false
        javaMaxHeapSize '4g'
    }

    buildTypes {
        release {
            proguardFiles 'proguard-android-optimize-wire.txt', 'proguard-rules.txt'
            minifyEnabled true
            multiDexEnabled true
            shrinkResources true
            manifestPlaceholders = [applicationVmSafeMode: "false",
                                    localyticsGcmSenderId: "\\ 826316279849"]
        }

        debug {
            // To get debugging properly working again - https://code.google.com/p/android/issues/detail?id=177480
            testCoverageEnabled = System.getenv("JOB_NAME") == "full-test-coverage"
            versionNameSuffix = " " + getDate()
            multiDexEnabled true
            manifestPlaceholders = [applicationVmSafeMode: "true",
                                    localyticsGcmSenderId: "\\ 826316279849"]
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "default"

    productFlavors {
        dev {
            applicationId "com.waz.zclient.dev"
            versionName majorVersion + android.defaultConfig.versionCode + "-dev"
            manifestPlaceholders = [applicationLabel       : "Wire Dev",
                                    applicationIcon        : "@drawable/ic_launcher_wire_dev",
                                    sharedUserId           : "",
                                    internal_features      : "true"]
            buildConfigField 'boolean', 'DEVELOPER_FEATURES_ENABLED', 'true'
            buildConfigField 'boolean', 'LOGGING_ENABLED',        'true'
            buildConfigField 'boolean', 'SAFE_LOGGING',     'false'
        }

        candidate {
            applicationId "com.wire.candidate"
            versionName majorVersion + android.defaultConfig.versionCode + "-candidate"
            manifestPlaceholders = [applicationLabel       : "Wire Candidate",
                                    applicationIcon        : "@drawable/ic_launcher_wire_candidate",
                                    sharedUserId           : "",
                                    internal_features      : "false"]

            buildConfigField 'boolean', 'DEVELOPER_FEATURES_ENABLED', 'true'
            buildConfigField 'boolean', 'LOGGING_ENABLED',        "$config.loggingEnabled"
            buildConfigField 'boolean', 'SAFE_LOGGING',     'true'
        }

        prod {
            applicationId config.applicationId
            manifestPlaceholders = [applicationLabel       : config.appName,
                                    applicationIcon        : "@drawable/ic_launcher_wire",
                                    sharedUserId           : config.userId,
                                    internal_features      : "false"]

            buildConfigField 'boolean', 'DEVELOPER_FEATURES_ENABLED', 'false'
            buildConfigField 'boolean', 'LOGGING_ENABLED',        "$config.loggingEnabled"
            buildConfigField 'boolean', 'SAFE_LOGGING',     'true'
        }

        internal {
            applicationId "com.wire.internal"
            versionName majorVersion + android.defaultConfig.versionCode + "-internal"
            manifestPlaceholders = [applicationLabel : "Wire Internal",
                                    applicationIcon  : "@drawable/ic_launcher_wire_internal",
                                    sharedUserId     : "",
                                    internal_features: "true"]

            buildConfigField 'boolean', 'DEVELOPER_FEATURES_ENABLED',    'true'
            buildConfigField 'boolean', 'LOGGING_ENABLED',        'true'
            buildConfigField 'boolean', 'SAFE_LOGGING',     'true'
        }

        experimental {
            applicationId "com.wire.x"
            versionName majorVersion + android.defaultConfig.versionCode + "-exp"
            manifestPlaceholders = [applicationLabel       : "Wire Exp",
                                    applicationIcon        : "@drawable/ic_launcher_wire_playground",
                                    sharedUserId           : "",
                                    internal_features      : "true"]

            buildConfigField 'boolean', 'DEVELOPER_FEATURES_ENABLED', 'true'
            buildConfigField 'boolean', 'LOGGING_ENABLED',        'true'
            buildConfigField 'boolean', 'SAFE_LOGGING',     'true'
        }
    }


    sourceSets {
        androidTest {
            java.srcDirs += ['build/generated/source/apt/androidTest/dev/debug']
            java.srcDirs += ['src/androidTest/kotlin']
        }

        test {
            scala.srcDirs += ['src/test/scala']
        }
    }

    /* to ignore error where multiple libs have this file */
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'APK_LICENSE.txt'
        exclude 'LICENSE.txt'
    }

}

dexinfo {
    maxDepth 2
}

dependencies {
    implementation project(':lintlib')

    //    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.scala-lang:scala-library:$scalaVersion"
    implementation "org.scala-lang:scala-reflect:$scalaVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    //wire libraries:
    implementation "com.wire:audio-notifications:$audioVersion"
    //don't include wire translations for custom builds
    if (customRepository.isEmpty() || token.isEmpty()) {
        implementation 'com.wire:wiretranslations:1.+'
    }

    // TODO  Nasty hack to be able to build add only one wire-core flavor to the build

    // TODO  (I know, it sucks, will think of a better way to do this, and we need this

    // TODO   when there are different SE APIs in dev vs internal/prod)
    boolean internal = true
    for (String taskName : gradle.startParameter.taskNames) {
        if (taskName.contains("Prod") || taskName.contains("Candidate") || taskName == "aPR") {
            internal = false
            break
        }
    }

    // For using local files in app/libs
    //implementation (name:'avs', ext:'aar')
    //implementation (name:'audio-notifications', ext:'aar')
    //implementation (name:'zmessaging-android', ext:'aar')

    implementation "$avsGroup:$avsName:${internal ? avsInternalVersion : avsVersion}"
    implementation("com.wire:zmessaging-android:${internal ? zMessagingDevVersion : zMessagingReleaseVersion}") {
        transitive = true
    }

    //support libraries
    implementation 'com.android.support:multidex:1.0.3'
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:support-v13:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:preference-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:gridlayout-v7:$supportLibVersion"
    implementation "com.android.support:support-annotations:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "android.arch.paging:runtime:$paging_version"
    implementation "com.android.support:exifinterface:$supportLibVersion"
    implementation "com.android.support:support-media-compat:$supportLibVersion"

    // Play services
    implementation "com.google.android.gms:play-services-base:$playServicesVersion"
    implementation "com.google.android.gms:play-services-maps:$playServicesVersion"
    implementation "com.google.android.gms:play-services-location:$playServicesVersion"
    implementation "com.google.android.gms:play-services-gcm:$playServicesVersion"
    implementation ('com.google.firebase:firebase-messaging:17.3.0') {
        exclude group: 'com.google.firebase', module: 'firebase-analytics'
        exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
    }

    //WorkManager
    def work_version = "1.0.0-alpha13"
    implementation "android.arch.work:work-runtime:$work_version"

    //third party libraries
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.evernote:android-job:1.2.6'
    implementation 'com.jakewharton.timber:timber:4.7.0'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'
    implementation 'com.facebook.rebound:rebound:0.3.8'
    implementation 'com.atlassian.commonmark:commonmark:0.11.0'
    implementation 'net.java.dev.jna:jna:4.4.0@aar'
    devImplementation "com.facebook.stetho:stetho:$stethoVersion"
    experimentalImplementation "com.facebook.stetho:stetho:$stethoVersion"
    internalImplementation "com.facebook.stetho:stetho:$stethoVersion"
    implementation "com.github.bumptech.glide:glide:4.8.0"
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'

    // Test dependencies
    testImplementation 'junit:junit:4.12'
    testImplementation("org.scalatest:scalatest_$scalaMajorVersion:2.2.6") {
        exclude module: 'scala-library'
    }
    testImplementation 'org.mockito:mockito-core:1.10.19'
    //The dexmaker stuff is needed for Mockito to work completely
    testImplementation 'com.crittercism.dexmaker:dexmaker:1.4'
    testImplementation 'com.crittercism.dexmaker:dexmaker-dx:1.4'
    testImplementation 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
    testImplementation "android.arch.paging:common:$paging_version"

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'org.mockito:mockito-core:1.10.19'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'

    //The dexmaker stuff is needed for Mockito to work completely
    androidTestImplementation 'com.crittercism.dexmaker:dexmaker:1.4'
    androidTestImplementation 'com.crittercism.dexmaker:dexmaker-dx:1.4'
    androidTestImplementation 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
    androidTestImplementation "android.arch.work:work-testing:$work_version"

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'org.mockito:mockito-core:1.10.19'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'

    ScalaCompileOptions.metaClass.daemonServer = true
    ScalaCompileOptions.metaClass.fork = true
    ScalaCompileOptions.metaClass.useAnt = false
    ScalaCompileOptions.metaClass.useCompileDaemon = false
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.additionalParameters = [
        "-feature",
        "-Xfuture",
        "-Xfatal-warnings",
        "-deprecation",
        "-Ywarn-unused-import",
        "-encoding",
        "UTF-8"]
}

static def getDate() {
    def date = new Date()
    def formattedDate = date.format('MM/dd HH:mm:ss')
    return formattedDate
}

static def buildSuffixForVersion(String versionString) {
    if (versionString.matches("\\d+\\.\\d+\\.\\d+(-[.0-9a-zA-Z]+)")) "" else "-DEV"
}

//add pretty naming to apk filename
android.applicationVariants.all { variant ->
    def appName
    //Check if an applicationName property is supplied; if not use the name of the parent project.
    if (project.hasProperty("applicationName")) {
        appName = applicationName
    } else {
        appName = "wire"
    }

    variant.outputs.each { output ->
        def newApkName
        newApkName = "${appName}-${output.baseName}-${majorVersion}${android.defaultConfig.versionCode}.apk"

        output.outputFileName = new File("../..", newApkName)
    }
}
