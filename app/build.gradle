import java.text.SimpleDateFormat
import org.ajoberstar.grgit.Grgit


apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'jp.leafytree.android-scala'
apply plugin: 'com.mutualmobile.gradle.plugins.dexinfo'
apply from: 'config/quality.gradle'
apply plugin: 'kotlinx-serialization'

task copyCustomResources(type: Copy) {
    if (buildtimeConfiguration.customResourcesPath == null) {
        return
    }

    def iconFolder = (buildtimeConfiguration.configuration.iconFolder ?: 'icons')
    def targetResFolder = "$rootDir/app/src/main/res"
    def iconSubFolders = [
            'ldpi': 'mipmap-ldpi',
            'mdpi': 'mipmap-mdpi',
            'hdpi': 'mipmap-hdpi',
            'xhdpi': 'mipmap-xhdpi',
            'xxhdpi': 'mipmap-xxhdpi',
            'xxxhdpi': 'mipmap-xxxhdpi'
    ]

    def resources = [
            "ic_launcher_wire.png" : (buildtimeConfiguration.configuration.launcherIcon ?: '')
    ]
    def customDir = buildtimeConfiguration.customResourcesPath
    resources.findAll { targetFileName, sourceFileName -> !sourceFileName.empty }
            .collectEntries { targetFileName, sourceFileName ->
                iconSubFolders.collectEntries { sourceSub, targetSub ->
                    [(customDir + '/' + iconFolder + '/' + sourceSub + '/' + sourceFileName) : [(targetResFolder + '/' + targetSub),  targetFileName]]
                }
            }
            .each { sourcePath, targetPath ->
                def source = file(sourcePath)
                assert source.exists()
                copy {
                    from source
                    rename { String filename -> return targetPath[1] }
                    into file(targetPath[0])
                }
                project.logger.info("Copied $sourcePath to ${targetPath[0]}/${targetPath[1]}")
            }

    copy {
        from file(customDir + '/strings/strings.xml') into file(targetResFolder + '/values')
        from file(customDir + '/strings/strings_no_translate.xml') into file(targetResFolder + '/values')
    }

    buildtimeConfiguration.configuration.languages.each { code ->
        def targetStringDir = "$targetResFolder/values-$code"
        copy {
            mkdir targetStringDir
            from file("$customDir/strings/$code/strings.xml") into file(targetStringDir)
        }
    }
}

android {
    //Trigger the licenseFormat task at least once in any compile phase
    applicationVariants.all { variant ->
        variant.javaCompiler.dependsOn(rootProject.licenseFormat)
    }

    preBuild.dependsOn(copyCustomResources)

    compileSdkVersion = Versions.COMPILE_SDK_VERSION
    buildToolsVersion = Versions.BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Versions.MIN_SDK_VERSION
        targetSdkVersion Versions.TARGET_SDK_VERSION
        versionCode readOrGenerateVersionCode() as Integer
        versionName = readOrGenerateVersionName() as String
        applicationId "com.waz.zclient"
        testInstrumentationRunner "com.waz.background.TestRunner"

        manifestPlaceholders = [
            customURLScheme   : buildtimeConfiguration.configuration.custom_url_scheme,
            applicationLabel  : "Wire Dev",
            applicationIcon   : "@mipmap/ic_launcher_wire_dev",
            sharedUserId      : ""
        ]

        buildConfigField 'Integer', 'MAX_ACCOUNTS',                  "$buildtimeConfiguration.configuration.maxAccounts"
        buildConfigField 'String',  'BACKEND_URL',                   "\"$buildtimeConfiguration.configuration.backendUrl\""
        buildConfigField 'String',  'WEBSOCKET_URL',                 "\"$buildtimeConfiguration.configuration.websocketUrl\""
        buildConfigField 'boolean', 'ACCOUNT_CREATION_ENABLED',      "$buildtimeConfiguration.configuration.allow_account_creation"
        buildConfigField 'boolean', 'ALLOW_ONLY_EMAIL_LOGIN',        "$buildtimeConfiguration.configuration.allow_only_email_login"
        buildConfigField 'boolean', 'ALLOW_SSO',                     "$buildtimeConfiguration.configuration.allowSSO"
        buildConfigField 'String',  'SUPPORT_EMAIL',                 "\"$buildtimeConfiguration.configuration.supportEmail\""
        buildConfigField 'String',  'FIREBASE_PUSH_SENDER_ID',       "\"$buildtimeConfiguration.configuration.firebasePushSenderId\""
        buildConfigField 'String',  'FIREBASE_APP_ID',               "\"$buildtimeConfiguration.configuration.firebaseAppId\""
        buildConfigField 'String',  'FIREBASE_API_KEY',              "\"$buildtimeConfiguration.configuration.firebaseApiKey\""
        buildConfigField 'boolean', 'ENABLE_BLACKLIST',              "$buildtimeConfiguration.configuration.enableBlacklist"
        buildConfigField 'String',  'BLACKLIST_HOST',                "\"$buildtimeConfiguration.configuration.blacklistHost\""
        buildConfigField 'String',  'CERTIFICATE_PIN_DOMAIN',        "\"$buildtimeConfiguration.configuration.certificatePin.domain\""
        buildConfigField 'String',  'CERTIFICATE_PIN_BYTES',         "\"$buildtimeConfiguration.configuration.certificatePin.certificate\""
        buildConfigField 'boolean', 'SUBMIT_CRASH_REPORTS',          "$buildtimeConfiguration.configuration.submitCrashReports"
        buildConfigField 'boolean', 'ALLOW_MARKETING_COMMUNICATION', "$buildtimeConfiguration.configuration.allowMarketingCommunication"
        buildConfigField 'boolean', 'ALLOW_CHANGE_OF_EMAIL',         "$buildtimeConfiguration.configuration.allowChangeOfEmail"
        buildConfigField 'String',  'CUSTOM_URL_SCHEME',             "\"$buildtimeConfiguration.configuration.custom_url_scheme\""
        buildConfigField 'Integer', 'NEW_PASSWORD_MINIMUM_LENGTH',   "$buildtimeConfiguration.configuration.new_password_minimum_length"
        buildConfigField 'Integer', 'NEW_PASSWORD_MAXIMUM_LENGTH',   "$buildtimeConfiguration.configuration.new_password_maximum_length"
        buildConfigField 'String',  'TEAMS_URL',                     "\"$buildtimeConfiguration.configuration.teamsUrl\""
        buildConfigField 'String',  'ACCOUNTS_URL',                  "\"$buildtimeConfiguration.configuration.accountsUrl\""
        buildConfigField 'String',  'WEBSITE_URL',                   "\"$buildtimeConfiguration.configuration.websiteUrl\""
        buildConfigField 'String',  'HTTP_PROXY_URL',                "\"$buildtimeConfiguration.configuration.http_proxy_url\""
        buildConfigField 'String',  'HTTP_PROXY_PORT',               "\"$buildtimeConfiguration.configuration.http_proxy_port\""
        buildConfigField 'boolean', 'BLOCK_ON_JAILBREAK_OR_ROOT',    "$buildtimeConfiguration.configuration.block_on_jailbreak_or_root"
        buildConfigField 'boolean', 'FORCE_HIDE_SCREEN_CONTENT',     "$buildtimeConfiguration.configuration.force_hide_screen_content"
        buildConfigField 'boolean', 'FORCE_APP_LOCK',                "$buildtimeConfiguration.configuration.force_app_lock"
        buildConfigField 'Integer', 'APP_LOCK_TIMEOUT',              "$buildtimeConfiguration.configuration.app_lock_timeout"
        buildConfigField 'boolean', 'BLOCK_ON_PASSWORD_POLICY',      "$buildtimeConfiguration.configuration.block_on_password_policy"
        buildConfigField 'boolean', 'WIPE_ON_COOKIE_INVALID',        "$buildtimeConfiguration.configuration.wipe_on_cookie_invalid"
        buildConfigField 'boolean', 'FORCE_PRIVATE_KEYBOARD',        "$buildtimeConfiguration.configuration.force_private_keyboard"
        buildConfigField 'Integer', 'PASSWORD_MAX_ATTEMPTS',         "$buildtimeConfiguration.configuration.password_max_attempts"
        buildConfigField 'boolean', 'DEVELOPER_FEATURES_ENABLED',    "$buildtimeConfiguration.configuration.developer_features_enabled"
        buildConfigField 'boolean', 'LOGGING_ENABLED',               "$buildtimeConfiguration.configuration.logging_enabled"
        buildConfigField 'boolean', 'SAFE_LOGGING',                  "$buildtimeConfiguration.configuration.safe_logging"
        buildConfigField 'boolean', 'FILE_RESTRICTION_ENABLED',      "$buildtimeConfiguration.configuration.file_restriction_enabled"
        buildConfigField 'String',  'FILE_RESTRICTION_LIST',         "\"$buildtimeConfiguration.configuration.file_restriction_list\""
        buildConfigField 'String',  'COUNTLY_SERVER_URL',            "\"$buildtimeConfiguration.configuration.countly_server_url\""
        buildConfigField 'String',  'COUNTLY_APP_KEY',               "\"$buildtimeConfiguration.configuration.countly_app_key\""
        buildConfigField 'boolean', 'KEEP_WEBSOCKET_ON',             "$buildtimeConfiguration.configuration.keep_websocket_on"
        buildConfigField 'boolean', 'FORCE_CONSTANT_BITRATE_CALLS',  "$buildtimeConfiguration.configuration.force_constant_bitrate_calls"
        buildConfigField 'boolean', 'WEB_LINK_PREVIEW',              "$buildtimeConfiguration.configuration.web_link_preview"

        //Feature Flags
        buildConfigField 'boolean', 'CONFERENCE_CALLING_RESTRICTION', "$conferenceCallingRestriction"
    }

    packagingOptions {
        // Need to exclude all armeabi/mips libs
        // as not all of our libs support these architectures
        exclude 'lib/armeabi/libgnustl_shared.so'
        exclude 'lib/mips/librs.blur.so'
        exclude 'lib/mips/librs.decode.so'
        exclude 'lib/mips/librsjni.so'
        exclude 'lib/mips/libRSSupport.so'
        exclude 'lib/armeabi/libjnidispatch.so'
        exclude 'lib/mips/libjnidispatch.so'
        exclude 'lib/mips64/libjnidispatch.so'
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            // All the usual Gradle options.
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
                exceptionFormat = 'full'
            }
        }
    }

    compileOptions {
        compileSdkVersion Versions.COMPILE_SDK_VERSION
        buildToolsVersion Versions.BUILD_TOOLS_VERSION

        sourceCompatibility Versions.SOURCE_COMPATIBILITY_VERSION
        targetCompatibility Versions.TARGET_COMPATIBILITY_VERSION
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    if (file("signing.gradle").exists()) {
        apply from: 'signing.gradle'
    }

    dexOptions {
        preDexLibraries false
        javaMaxHeapSize '4g'
    }

    buildTypes {
        release {
            testCoverageEnabled = false
            proguardFiles 'proguard-android-optimize-wire.txt', 'proguard-rules.txt'
            minifyEnabled false
            //shrinkResources true
            manifestPlaceholders = [applicationVmSafeMode: "false",
                                    localyticsGcmSenderId: "\\ 826316279849"]
        }

        debug {
            testCoverageEnabled = false
            applicationIdSuffix = '.dev'
            proguardFiles 'proguard-android-optimize-wire.txt', 'proguard-rules.txt'
            minifyEnabled false
            //shrinkResources true
            manifestPlaceholders = [applicationVmSafeMode: "false",
                                    localyticsGcmSenderId: "\\ 826316279849"]
        }
    }

    flavorDimensions "default"

    productFlavors {
        dev {
            applicationId buildtimeConfiguration.configuration.dev.applicationId
            versionName android.defaultConfig.versionName + buildtimeConfiguration.configuration.dev.version_name_postfix
            manifestPlaceholders = [applicationLabel       : buildtimeConfiguration.configuration.dev.appName,
                                    applicationIcon        : "@mipmap/$buildtimeConfiguration.configuration.dev.launcherIcon",
                                    sharedUserId           : buildtimeConfiguration.configuration.dev.userId]

            buildConfigField 'boolean', 'DEVELOPER_FEATURES_ENABLED', "$buildtimeConfiguration.configuration.dev.developer_features_enabled"
            buildConfigField 'boolean', 'LOGGING_ENABLED', "$buildtimeConfiguration.configuration.dev.logging_enabled"
            buildConfigField 'boolean', 'SAFE_LOGGING', "$buildtimeConfiguration.configuration.dev.safe_logging"
        }

        candidate {
            applicationId buildtimeConfiguration.configuration.candidate.applicationId
            manifestPlaceholders = [applicationLabel       : buildtimeConfiguration.configuration.candidate.appName,
                                    applicationIcon        : "@mipmap/$buildtimeConfiguration.configuration.candidate.launcherIcon",
                                    sharedUserId           : buildtimeConfiguration.configuration.candidate.userId]

            buildConfigField 'boolean', 'DEVELOPER_FEATURES_ENABLED', "$buildtimeConfiguration.configuration.candidate.developer_features_enabled"
            buildConfigField 'boolean', 'LOGGING_ENABLED', "$buildtimeConfiguration.configuration.candidate.logging_enabled"
            buildConfigField 'boolean', 'SAFE_LOGGING', "$buildtimeConfiguration.configuration.candidate.safe_logging"
        }

        prod {
            applicationId buildtimeConfiguration.configuration.prod.applicationId
            manifestPlaceholders = [applicationLabel       : buildtimeConfiguration.configuration.prod.appName,
                                    applicationIcon        : "@mipmap/$buildtimeConfiguration.configuration.prod.launcherIcon",
                                    sharedUserId           : buildtimeConfiguration.configuration.prod.userId]

            buildConfigField 'boolean', 'DEVELOPER_FEATURES_ENABLED', "$buildtimeConfiguration.configuration.prod.developer_features_enabled"
            buildConfigField 'boolean', 'LOGGING_ENABLED', "$buildtimeConfiguration.configuration.prod.logging_enabled"
            buildConfigField 'boolean', 'SAFE_LOGGING', "$buildtimeConfiguration.configuration.prod.safe_logging"
        }

        fdroid {
            applicationId buildtimeConfiguration.configuration.fdroid.applicationId
            manifestPlaceholders = [applicationLabel       : buildtimeConfiguration.configuration.fdroid.appName,
                                    applicationIcon        : "@mipmap/$buildtimeConfiguration.configuration.fdroid.launcherIcon",
                                    sharedUserId           : buildtimeConfiguration.configuration.fdroid.userId]

            buildConfigField 'boolean', 'DEVELOPER_FEATURES_ENABLED', "$buildtimeConfiguration.configuration.fdroid.developer_features_enabled"
            buildConfigField 'boolean', 'LOGGING_ENABLED', "$buildtimeConfiguration.configuration.fdroid.logging_enabled"
            buildConfigField 'boolean', 'SAFE_LOGGING', "$buildtimeConfiguration.configuration.fdroid.safe_logging"

            buildConfigField 'boolean', 'KEEP_WEBSOCKET_ON', "$buildtimeConfiguration.configuration.fdroid.keep_websocket_on"
        }

        internal {
            applicationId buildtimeConfiguration.configuration.internal.applicationId
            versionName android.defaultConfig.versionName + buildtimeConfiguration.configuration.internal.version_name_postfix
            manifestPlaceholders = [applicationLabel       : buildtimeConfiguration.configuration.internal.appName,
                                    applicationIcon        : "@mipmap/$buildtimeConfiguration.configuration.internal.launcherIcon",
                                    sharedUserId           : buildtimeConfiguration.configuration.internal.userId]

            buildConfigField 'boolean', 'DEVELOPER_FEATURES_ENABLED', "$buildtimeConfiguration.configuration.internal.developer_features_enabled"
            buildConfigField 'boolean', 'LOGGING_ENABLED', "$buildtimeConfiguration.configuration.internal.logging_enabled"
            buildConfigField 'boolean', 'SAFE_LOGGING', "$buildtimeConfiguration.configuration.internal.safe_logging"
        }

        experimental {
            applicationId buildtimeConfiguration.configuration.experimental.applicationId
            versionName android.defaultConfig.versionName + buildtimeConfiguration.configuration.experimental.version_name_postfix
            manifestPlaceholders = [applicationLabel       : buildtimeConfiguration.configuration.experimental.appName,
                                    applicationIcon        : "@mipmap/$buildtimeConfiguration.configuration.experimental.launcherIcon",
                                    sharedUserId           : buildtimeConfiguration.configuration.experimental.userId]

            buildConfigField 'boolean', 'DEVELOPER_FEATURES_ENABLED', "$buildtimeConfiguration.configuration.experimental.developer_features_enabled"
            buildConfigField 'boolean', 'LOGGING_ENABLED', "$buildtimeConfiguration.configuration.experimental.logging_enabled"
            buildConfigField 'boolean', 'SAFE_LOGGING', "$buildtimeConfiguration.configuration.experimental.safe_logging"
        }
    }


    sourceSets {
        if (fdroidBuild) {
            main.java.srcDirs += ['src/foss/java', 'src/foss/scala']
        } else {
            main.java.srcDirs += ['src/nonfree/java', 'src/nonfree/scala']
        }

        main.java.srcDirs += ['src/main/java', 'src/main/kotlin']

        androidTest {
            java.srcDirs += ['build/generated/source/apt/androidTest/dev/debug']
            java.srcDirs += ['src/androidTest/kotlin']
        }

        test {
            java.srcDirs += ['src/test/java', 'src/test/kotlin', 'src/test/scala']
        }
    }

    /* to ignore error where multiple libs have this file */
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'APK_LICENSE.txt'
        exclude 'LICENSE.txt'
        exclude 'META-INF/proguard/coroutines.pro'
    }
}

dexinfo {
    maxDepth 2
}

dependencies {

    implementation LegacyDependencies.scalaLibrary
    implementation LegacyDependencies.scalaReflect
    implementation LegacyDependencies.scalaShapeless


    implementation BuildDependencies.kotlin.standardLibrary
    implementation BuildDependencies.kotlin.coroutinesAndroid
    implementation BuildDependencies.kotlin.coroutinesCore
    implementation avsDependency

    //don't include wire translations for custom builds
    if (buildtimeConfiguration.customResourcesPath == null) {
        implementation BuildDependencies.wire.translations
    }

    implementation BuildDependencies.libPhoneNumber

    api project(ModuleDependencies.storage)
    api(project(ModuleDependencies.syncEngine)) {
        exclude group: avsGroup, module: avsName
    }

    //Android
    annotationProcessor BuildDependencies.androidX.annotation
    implementation BuildDependencies.androidX.material
    implementation BuildDependencies.androidX.appCompat
    implementation BuildDependencies.androidX.recyclerView
    implementation BuildDependencies.androidX.preference
    implementation BuildDependencies.androidX.cardView
    implementation BuildDependencies.androidX.gridLayout
    implementation BuildDependencies.androidX.constraintLayout
    implementation BuildDependencies.androidX.paging
    implementation BuildDependencies.androidX.exifInterface
    implementation BuildDependencies.androidX.media
    implementation(BuildDependencies.androidX.lifecycleRuntime) {
        exclude group: 'org.jetbrains.kotlinx', module: 'kotlinx-coroutines-core'
    }
    implementation (BuildDependencies.androidX.lifecycleLiveData) {
        exclude group: 'org.jetbrains.kotlinx', module: 'kotlinx-coroutines-core'
    }
    implementation (BuildDependencies.androidX.lifecycleViewModel) {
        exclude group: 'org.jetbrains.kotlinx', module: 'kotlinx-coroutines-core'
    }
    implementation BuildDependencies.androidX.lifecycleExtensions
    implementation BuildDependencies.androidX.roomRuntime
    implementation (BuildDependencies.androidX.roomKtx) {
        exclude group: 'org.jetbrains.kotlinx', module: 'kotlinx-coroutines-core'
    }
    implementation BuildDependencies.androidX.coreKtx
    implementation BuildDependencies.androidX.biometric
    implementation(BuildDependencies.androidX.workManager) {
        exclude group: 'androidx.room', module: 'room-runtime'
    }

    implementation BuildDependencies.androidX.viewPager2

    if (!fdroidBuild) {
        // Play services
        implementation BuildDependencies.playServices.base
        implementation BuildDependencies.playServices.maps
        implementation BuildDependencies.playServices.location
        implementation BuildDependencies.playServices.gcm

        // Firebase
        implementation (BuildDependencies.fireBaseMessaging) {
            exclude group: 'com.google.firebase', module: 'firebase-analytics'
            exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
        }
    }

    //Glide
    implementation(BuildDependencies.glide.core){
        exclude group: 'com.android.support'
        transitive = true
    }
    annotationProcessor BuildDependencies.glide.compiler

    //Retrofit
    implementation (BuildDependencies.retrofit.core) {
        exclude group: 'com.google.protobuf', module: 'protobuf-javalite'
        exclude group: 'com.google.protobuf', module: 'protobuf-lite'
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }
    implementation BuildDependencies.retrofit.gsonConverter

    //OkHttp
    implementation BuildDependencies.okHttpLoggingInterceptor

    //DI - Koin
    implementation BuildDependencies.koin.androidCore
    implementation BuildDependencies.koin.androidScope
    implementation BuildDependencies.koin.androidViewModel

    // RxJava
    implementation BuildDependencies.rxJava.rxKotlin
    implementation BuildDependencies.rxJava.rxAndroid

    //JSON
    implementation BuildDependencies.kotlinXSerialization

    //third party libraries
    implementation BuildDependencies.androidJob
    implementation(BuildDependencies.threetenbpAndroid) {
        exclude group: 'org.threeten', module: 'threetenbp'
    }
    implementation BuildDependencies.threetenbpJava
    implementation BuildDependencies.rebound
    implementation BuildDependencies.commonMark
    implementation BuildDependencies.jna

    if (!fdroidBuild) {
        implementation BuildDependencies.countly
    }

    implementation BuildDependencies.wireSignals
    implementation BuildDependencies.wireSignalsExtensions

    implementation BuildDependencies.libSodium

    implementation BuildDependencies.zooming

    implementation BuildDependencies.osmdroid
    implementation(BuildDependencies.osmbonuspack) {
        // don't bring in newer com.squareup.okhttp3:okhttp & androidx.core:core
        transitive = false
    }

    // Unit Tests
    testImplementation TestDependencies.jUnit
    testImplementation TestDependencies.mockito.core
    testImplementation TestDependencies.mockito.inline
    testImplementation TestDependencies.kluent
    testImplementation TestDependencies.kotlin.coroutinesTest
    testImplementation BuildDependencies.kotlin.coroutinesCore
    testImplementation TestDependencies.androidX.testCore
    testImplementation TestDependencies.okHttpMockWebServer
    testImplementation(LegacyDependencies.scalaTest) {
        exclude module: 'scala-library'
        exclude group: 'org.jetbrains.kotlinx', module: 'kotlinx-coroutines-core'
    }
    testImplementation project(ModuleDependencies.commonTest)
    testImplementation LegacyDependencies.scalaMock
    testImplementation LegacyDependencies.scalaCheck

    // Functional Tests
    androidTestImplementation TestDependencies.jUnit
    androidTestImplementation TestDependencies.mockito.core
    androidTestImplementation TestDependencies.kluent
    androidTestImplementation TestDependencies.androidX.testJunit
    androidTestImplementation TestDependencies.androidX.testRules
    androidTestImplementation TestDependencies.androidX.testWorkManager


    // Legacy Scala
    ScalaCompileOptions.metaClass.daemonServer = true
    ScalaCompileOptions.metaClass.fork = true
    ScalaCompileOptions.metaClass.useAnt = false
    ScalaCompileOptions.metaClass.useCompileDaemon = false
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.additionalParameters = [
        "-feature",
        "-Xfuture",
        "-Xfatal-warnings",
        "-deprecation",
        "-Ywarn-unused-import",
        "-encoding",
        "UTF-8"]
}

tasks.register("generateVersionFile") {
    //clean the repo from a probably existing version.txt
    if(file("$projectDir/version.txt").exists()) {
        println("deleting existing version.txt file for safety reasons")
        delete file "$projectDir/version.txt"
    }
    def versionCode = readOrGenerateVersionCode()
    def versionName = readOrGenerateVersionName()

    println("VersionCode = " + versionCode)
    println("VersionName = " + versionName)
    doLast {
        new File(projectDir, "version.txt").text = """VersionCode: $versionCode
VersionName: $versionName
Revision: ${grgit.head().abbreviatedId}
Buildtime: ${new SimpleDateFormat("dd-MM-yyyy HH:mm:ss").format(new Date())}
Application-name: com.wire
    """
    }
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('MM/dd HH:mm:ss')
    return formattedDate
}

def readOrGenerateVersionCode() {
    if(!file("$projectDir/version.txt").exists()) {
        println("No version.txt found, generating new versioncode")
        return generateUnixTimestamp()
    }

    //file exists, read the version code from the version.txt
    println("version.txt found, using versionCode from version.txt file")
    String versionContext = new File("$projectDir/version.txt").text
    def matcher = versionContext =~ /VersionCode: (.*)/
    matcher.find()
    def versionCode = matcher.group(1)
    println("Found VersionCode in File: $versionCode")
    return versionCode
}

def readOrGenerateVersionName() {
    if(!file("$projectDir/version.txt").exists()) {
        println("No version.txt found, using system internal version name")
        def patchVersion = System.getenv("PATCH_VERSION") as Integer?: System.getenv("BUILD_NUMBER") as Integer ?: 99999
        return "$Versions.ANDROID_CLIENT_MAJOR_VERSION$patchVersion"
    }

    //file exists, read the versionanme from the version.txt
    String versionContext = new File("$projectDir/version.txt").text
    println("$versionContext")
    def matcher = versionContext =~ /VersionName: (.*)/
    matcher.find()
    def versionName = matcher.group(1)
    println("Found VersionName found in File: $versionName")
    return versionName
}

def generateUnixTimestamp() {
    def dateOffset = new Date("04/21/2021 01:00:00")
    def date = new Date()
    def dateTime = (date.getTime()/10000L)
    def dateTimeOffset = (dateOffset.getTime()/10000L)
    def versionCode = (int)(dateTime-dateTimeOffset)
    println("VERSIONCODE for ("+date.dateTimeString+"):"+versionCode)
    return versionCode
}

def buildSuffixForVersion(String versionString) {
    if (versionString.matches("\\d+\\.\\d+\\.\\d+(-[.0-9a-zA-Z]+)")) "" else "-DEV"
}

//add pretty naming to apk filename
android.applicationVariants.all { variant ->
    def appName
    //Check if an applicationName property is supplied; if not use the name of the parent project.
    if (project.hasProperty("applicationName")) {
        appName = applicationName
    } else {
        appName = "wire"
    }

    variant.outputs.each { output ->
        def newApkName
        if(buildtimeConfiguration.isCustomBuild()) {
            newApkName = "${appName}-${buildtimeConfiguration.configuration.applicationId}-${output.baseName}-${android.defaultConfig.versionName}.apk"
        } else {
            newApkName = "${appName}-${output.baseName}-${android.defaultConfig.versionName}.apk"
        }
        output.outputFileName = new File("../..", newApkName)
    }
}

configurations.all {
    resolutionStrategy.eachDependency {
        if (requested.module.toString().contains('androidx.sqlite:sqlite')) {
            useVersion "2.2.0"
        }
    }
}
